apply plugin: 'com.android.library'

version = VERSION_NAME_SDK
android {
    compileSdkVersion 'android-Q'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        buildConfigField "String", "TDSDK_VERSION", "\"${version}\""
        consumerProguardFile 'proguard-rules.pro'

        testApplicationId "cn.thinkingdata.analytics"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled true
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    testImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha01'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha01'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
}

// 创建 Jar 包
def SDK_BASENAME ="ThinkingAnalyse";
def SDK_VERSION ="-${version}";
def sdkDestinationPath ="build/outputs/jar/";
def zipFile = file('build/intermediates/packaged-classes/release/classes.jar')

task deleteBuild(type: Delete) {
    delete sdkDestinationPath + SDK_BASENAME + SDK_VERSION +".jar"
}

task makeJar(type: Jar) {
    from zipTree(zipFile)
    from fileTree(dir:'src/main', includes: ['assets/**'])// 打包assets目录下的所有文件
    baseName = SDK_BASENAME
    destinationDir = file(sdkDestinationPath)
}

makeJar.dependsOn(deleteBuild, build)

// 发布新版本 bintrayUpload
apply plugin: 'com.novoda.bintray-release'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
publish {
    bintrayUser = properties.getProperty("bintray.user")
    bintrayKey = properties.getProperty("bintray.apikey")
    dryRun = false
    userOrg = POM_ORGANIZATION
    repoName = POM_REPO_NAME
    groupId = POM_GROUP_ID
    artifactId = 'ThinkingAnalyticsSDK'
    publishVersion = version
    desc = 'The official Android SDK for Thinking Data Analytics'
    website = POM_WEBSITE
}

// 上传到本地代码库
apply plugin: 'maven'
uploadArchives{
    repositories{
        mavenDeployer{
            repository(url:uri('../repo'))
            pom.groupId = POM_GROUP_ID
            pom.artifactId = 'ThinkingAnalyticsSDK'
            pom.version = version
        }
    }
}