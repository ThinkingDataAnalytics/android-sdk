apply plugin: 'com.android.library'

version = VERSION_NAME
android {
    compileSdkVersion 'android-Q'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 28
        versionCode 1
        buildConfigField "String", "TDSDK_VERSION", "\"${version}\""
        consumerProguardFile 'proguard-rules.pro'
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled true
        }
        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.aspectj:aspectjrt:1.8.10'
}

// 创建 Jar 包
def SDK_BASENAME ="ThinkingAnalyse";
def SDK_VERSION ="-${version}";
def sdkDestinationPath ="build/outputs/jar/";
def zipFile = file('build/intermediates/packaged-classes/release/classes.jar')

task deleteBuild(type: Delete) {
    delete sdkDestinationPath + SDK_BASENAME + SDK_VERSION +".jar"
}

task makeJar(type: Jar) {
    from zipTree(zipFile)
    from fileTree(dir:'src/main', includes: ['assets/**'])// 打包assets目录下的所有文件
    baseName = SDK_BASENAME
    destinationDir = file(sdkDestinationPath)
}

makeJar.dependsOn(deleteBuild, build)

// 发布新版本 bintrayUpload
apply plugin: 'com.novoda.bintray-release'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
publish {
    bintrayUser = properties.getProperty("bintray.user")
    bintrayKey = properties.getProperty("bintray.apikey")
    dryRun = false
    userOrg = 'thinkinggame'
    groupId = 'com.thinking.analyselibrary'
    artifactId = 'ThinkingAnalyticsSDK'
    publishVersion = version
    desc = 'The official Android SDK for Thinking Data Analytics'
    website =  'https://github.com/thinkinggame/ThinkingAnalyticsSDK'
}


// 上传到本地代码库
apply plugin: 'maven'
uploadArchives{
    repositories{
        mavenDeployer{
            repository(url:uri('../repo'))
            pom.groupId = 'com.thinking.analyselibrary'
            pom.artifactId = 'ThinkingAnalyticsSDK'
            pom.version = version
        }
    }
}